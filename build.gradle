plugins {
    id 'java'
    id "de.undercouch.download" version "3.4.3"
}

ext {
    fusionAuthVersion = "1.6.1"
    println "Using FA version: <<$fusionAuthVersion>>"
    fusionAuthDir = new File(project.buildDir, "fusionauth")
    faDepRepoDir = new File(fusionAuthDir, "/fusionauth-app/web/WEB-INF/lib")
}

repositories {
    mavenCentral()
}

import de.undercouch.gradle.tasks.download.Download

task downloadFaJar(type: Download) {
    src "https://storage.googleapis.com/inversoft_products_j098230498/products/fusionauth/${fusionAuthVersion}/fusionauth-app-${fusionAuthVersion}.zip"
    dest new File(fusionAuthDir, "fa-dist.zip")
    onlyIfModified true
}

task prepareFaLibs(dependsOn: downloadFaJar, type: Copy) {
    from zipTree(downloadFaJar.dest)
    into fusionAuthDir
}

compileJava.dependsOn prepareFaLibs

version = '0.0.1-SNAPSHOT'
group = "com.github.filipmalczak"

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    //https://fusionauth.io/docs/v1/tech/plugins/writing-a-plugin
    //this tutorial uses maven dependencies, but they seem to be unresolvable
    //its probably gradle/savant issue
    //for now lets stay with downloaded files
    compile fileTree(include: ['*.jar'], dir: faDepRepoDir)
    compile fileTree(include: ['*.jar'], dir: new File(fusionAuthDir, "/fusionauth-app/apache-tomcat/lib"))
}

jar {
    archiveClassifier.set("plugin")
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.4.1'
}